plugins {
    id 'com.android.application'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.android'
}

android {
    def calendar = Calendar.getInstance()

    def code = String.format("%d%02d%02d", calendar.get(Calendar.YEAR), calendar.get(Calendar.MONTH) + 1, calendar.get(Calendar.DAY_OF_MONTH))

    compileSdk 33

    defaultConfig {
        applicationId "com.zxhhyj.music"
        minSdk 23
        targetSdk 33
        versionCode Integer.parseInt(code)
        versionName "${calendar.get(Calendar.YEAR)}.${calendar.get(Calendar.MONTH) + 1}.${calendar.get(Calendar.DAY_OF_MONTH)}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "arm64-v8a", "x86_64", "x86"
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion "$compose_compiler_version"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose true
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

    namespace 'com.zxhhyj.music'
}

dependencies {
    implementation fileTree(include: ['*.*'], dir: 'libs')
    // https://github.com/flavioarfaria/KenBurnsView
    implementation 'com.flaviofaria:kenburnsview:1.0.7'
    // https://exoplayer.dev/
    implementation 'com.google.android.exoplayer:exoplayer-core:2.18.5'
    // https://github.com/coil-kt/coil/blob/main/README-zh.md
    implementation "io.coil-kt:coil-compose:2.3.0"
    // https://github.com/google/gson
    implementation 'com.google.code.gson:gson:2.10'
    // https://mvnrepository.com/artifact/net.jthink/jaudiotagger
    implementation 'net.jthink:jaudiotagger:3.0.1'
    // https://github.com/olshevski/compose-navigation-reimagined
    def reimagined_version = "1.4.0"
    implementation "dev.olshevski.navigation:reimagined:$reimagined_version"
    implementation "dev.olshevski.navigation:reimagined-material:$reimagined_version"
    // https://github.com/mxalbert1996/compose-shared-elements
    implementation "com.mxalbert.sharedelements:shared-elements:0.1.0-SNAPSHOT"

    // https://github.com/FunnySaltyFish/ComposeDataSaver
    def data_saver_version = "1.1.4"
    implementation "com.github.FunnySaltyFish.ComposeDataSaver:data-saver:$data_saver_version"
    implementation "com.github.FunnySaltyFish.ComposeDataSaver:data-saver-mmkv:$data_saver_version"
    implementation 'com.tencent:mmkv:1.2.16'

    // https://github.com/krottv/compose-sliders
    implementation "com.github.krottv:compose-sliders:0.1.4"

    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.palette:palette:1.0.0'

    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"

    def kotlinx_coroutines_version = "1.7.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"

    implementation "androidx.compose.runtime:runtime-livedata:$compose_ui_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_ui_version"
    implementation "androidx.compose.material:material:$compose_ui_version"
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    //implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"

    configurations { all*.exclude group: 'androidx.appcompat', module: 'appcompat' }
}